#! /usr/bin/perl
# vim:et:sw=4:ts=4

use strict;
use English;
use Getopt::Long qw(:config no_ignore_case bundling);
use File::Basename;
use LWP::UserAgent;

our $VERSION = '1.4.2';
our %GlobalConfig = (
    debug => 0,

    # naemon perfdata settings
    perfdata => "/var/lib/naemon/service-perfdata",

    # old way to specify performance line format
    separator => "",
    perfformat => "",
    perfdelimiter => "",

    # new way to specify performance line format
    perflineregexp => "",
    time_is_captured_as => "time",
    data_is_captured_as => "data",

    # feature enabling flags
    autotagappender => 0,
    enable_optional_values => 0,
);

our %ConditionalConfigTemplate = (
    cond => undef,

    # influxdb measurement name settings
    global_prefix => undef,
    trimpattern => undef,
    measurement => undef,

    # influxdb field key settings
    timereplacement => undef,
    timefactor      => 9,
    fieldkey        => undef,

    # influxdb server settings
    output         => undef,
    database       => undef,  # v1 only
    username       => undef,  # v1 only
    password       => undef,  # v1 only
    apiver         => undef,
    org            => undef,  # v2 only
    bucket         => undef,  # v2 only
    attibutebacket => undef,  # v2 only
    token          => undef,  # v2 only
);

our @ConditionalConfig = (
    {
	%ConditionalConfigTemplate,
	# overwrite by default values
        "*cond"         => "1",
	timereplacement => "Time",
        timefactor      => 9,
	output          => "http://localhost:8086/",
	apiver          => 1,
    },
);

our $OS_ERROR;

our $PRESERVE;
our $DRYRUN;

our %LogLvl = (
    "Decode"   => 64,
    "Data"     => 32,
    "Debug"    => 16,
    "Settings" => 8,
    "Files"    => 4,
    "Output"   => 2,
    "HTTP"     => 1,
);

sub Usage {
	my $prog = basename($0);
	print "$prog\t[-d|--debug debug] [-D|--perfdata file]\n";
	print "\t\t[-f|--file file] [-n|--preserve] [-N|--dryrun]\n";
	print "\t\t[-T|--timefactor]\n";
	print "$prog [-h|--help]\n";
}

sub checkFile {
    my ($type, $filename) = @_;
    die "No $type file specified\n" unless ($filename);
    die "$filename: file not found\n" unless (-e $filename);
    die "$filename: permission denied\n" unless (-r $filename);
}

sub Log {
    my ($lvl, $msg) = @_;
    my $prog = basename($0);
    if ($GlobalConfig{debug} & $lvl) { printf STDERR "[%3d] %s: %s\n", $lvl, $prog, $msg; };
}

sub cmdlineOverride {
    my ($debug, $perfdata, $timefactor) = @_;
    $GlobalConfig{debug} = $debug if ($debug);
    $GlobalConfig{perfdata} = $perfdata if ($perfdata);
    $ConditionalConfig[0]{timefactor} = $timefactor if ($timefactor);
}

sub readConfig {
    my ($filename) = @_;
    open my $FH, '<', $filename or ## no critic (RequireBriefOpen)
        die "cannot open $filename: $OS_ERROR, stopped";
    my $curr_conditionalconfig = $ConditionalConfig[0];
    while (<$FH>) {
        next if /^\s*#/;        # skip commented lines
        s/^\s+//;               # removes leading whitespace
        /^\s*([^=\s]+)\s*=\s*(|.*\S)\s*$/x and do { # splits into key=val pairs
            my $key = $1;
            my $val = $2;
	    if    ( exists $GlobalConfig{$key} ){
            	$GlobalConfig{$key} = $val;
	    }elsif( exists $$curr_conditionalconfig{$key} ){
            	$$curr_conditionalconfig{$key} = $val;
	    }else{
		Log $LogLvl{Settings}, "$filename:$.: unknown key '$key'";
	    }
        };
        /^\s*\*\s* if (
	    (?:
		\s+
		(?:
		    <\w+>|
		    =~|!~|==|!=|<|>|eq|ne|gt|lt|not|and|or|\(|\) |
		    "[^\\"\\x00-\\x1f]*" |
		    [-+]?\d+(\.\d+)?
	        )
	    )+
        )\s*$/x and do {
	    my $cond = $1;
	    $cond =~ s{
		\s+
		(?:
		    <(\w+)>|
		    ( =~|!~|==|!=|<|>|eq|ne|gt|lt|not|and|or|\(|\) ) |
		    (" [^\\"\\x00-\\x1f]* ") |
		    ([-+]?\d+(\.\d+)?)
		)
	    }{
		if    ( $1 ne "" ){ # variable
		    " \$var{\"$1\"}";
		}elsif( $2 ne "" ){ # operator
		    " $2";
		}elsif( $3 ne "" ){ # text
		    " $3";
		}elsif( $4 ne "" ){ # number
		    " $4";
		}else{ undef; }
	    }egx;
	    $curr_conditionalconfig = {
		   %ConditionalConfigTemplate,
		   "*cond" => $cond,
	    };
	    push @ConditionalConfig, $curr_conditionalconfig;
	}
    }
    close $FH or return "close failed for $filename: $OS_ERROR";
    Log $LogLvl{Files}, "Config:$filename";
}

sub output_v1 {
    my ($config, $data_array) = @_;
    my $data;
    my $timefactor = 10 ** $$config{timefactor};
    foreach my $i ( @$data_array ){
	my ($time, $measurement, $fieldkey, $value) = @$i;
	$time = $time * $timefactor;
	$data .= "$measurement $fieldkey=$value $time\n";
    }
    my $ua = LWP::UserAgent->new;
     
    my $url = $$config{output};
    $url =~ s{/$}{};
    my $server_endpoint = "$url/write?db=".$$config{database}."&u=".$$config{username}."&p=".$$config{password};
    my $req = HTTP::Request->new(POST => $server_endpoint);
    Log $LogLvl{Output}, $data;
     
    # add POST data to HTTP request body
    if  ($DRYRUN) {
        Log $LogLvl{HTTP}, sprintf "%s [%s] (%d)", $server_endpoint, "-0-", length($data);
	print $data, "\n";
        return 1;
    }

    $req->content($data);
    my $resp = $ua->request($req);
    if ($resp->is_success) {
        my $message = $resp->decoded_content;
        Log $LogLvl{HTTP}, sprintf "%s [%s] (%d)", $server_endpoint, $resp->code, length($data);
	return 1;
    }
    else {
        Log $LogLvl{HTTP}, sprintf "HTTP POST error endpoint: %s", $server_endpoint;
        Log $LogLvl{HTTP}, sprintf "HTTP POST error status line: %s", $resp->status_line;
        Log $LogLvl{HTTP}, sprintf "HTTP POST error response body: %s", $resp->content;
	return undef;
    }
}

sub output_v2 {
    my ($config, $data_array) = @_;
    my $data;
    my $timefactor = 10 ** $$config{timefactor};
    foreach my $i ( @$data_array ){
	my ($time, $measurement, $fieldkey, $value) = @$i;
	$time = $time * $timefactor;
	$data .= "$measurement $fieldkey=$value $time\n";
    }
    my $ua = LWP::UserAgent->new;
     
    my $url = $$config{output};
    $url =~ s{/$}{};
    my $server_endpoint = "$url/api/v2/write?org=".$$config{org}."&bucket=".$$config{bucket};
    my $req = HTTP::Request->new(POST => $server_endpoint);
    $req->header("Authorization" => "Token ".$$config{token});
    $req->header("Content-Type"  => "text/plain; charset=utf-8");
    $req->header("Accept"        => "application/json");
    Log $LogLvl{Output}, $data;
     
    # add POST data to HTTP request body
    if  ($DRYRUN) {
        Log $LogLvl{HTTP}, sprintf "%s [%s] (%d)", $server_endpoint, "-0-", length($data);
	print $data, "\n";
        return 1;
    }

    $req->content($data);
    my $resp = $ua->request($req);
    if ($resp->is_success) {
        my $message = $resp->decoded_content;
        Log $LogLvl{HTTP}, sprintf "%s [%s] (%d)", $server_endpoint, $resp->code, length($data);
	return 1;
    }
    else {
        Log $LogLvl{HTTP}, sprintf "HTTP POST error endpoint: %s", $server_endpoint;
        Log $LogLvl{HTTP}, sprintf "HTTP POST error status line: %s", $resp->status_line;
        Log $LogLvl{HTTP}, sprintf "HTTP POST error response body: %s", $resp->content;
	return undef;
    }
}

sub output {
    my ( @data ) = @_;
    
    my %destname2config;
    my %destname2data;
    foreach my $data ( @data ){
	my @metric = @{ $$data{"*metric"} };
	foreach my $metric ( @metric ){
	    my %curr_config;
	    my %var = ( %$data, %$metric );
	    foreach my $config ( @ConditionalConfig ){
	        my $cond = eval $$config{"*cond"};
	        next if $@;
	        next unless $cond;

	        while( my ($k, $v) = each %$config ){ $curr_config{$k} = $v if defined $v; }
	    }

	    my $destname = join " ", $curr_config{output}, $curr_config{apiver};
	    if    ( $curr_config{apiver} == 1 ){
	   	$destname = join " ", $destname,
			$curr_config{database},
			$curr_config{username},
			$curr_config{password};
	    }elsif( $curr_config{apiver} == 2 ){
	   	$destname = join " ", $destname,
			$curr_config{org},
			$curr_config{bucket},
			$curr_config{token};
	    }else{ die; }

	    unless( defined $destname2config{$destname} ){
		$destname2config{$destname} = \%curr_config;
	    }

	    my $measurement = $curr_config{measurement};
	    my $fieldkey    = $curr_config{fieldkey};
	    $measurement =~ s{<(\w+)>}{ $var{$1}; }eg;
	    $fieldkey    =~ s{<(\w+)>}{ $var{$1}; }eg;
	    $fieldkey = $curr_config{timereplacement} if $fieldkey eq 'time';
	    push @{$destname2data{$destname}}, [
		$var{"*time"}, $measurement, $fieldkey, $var{"value"}
	    ];
	}
    }

    my $ok = 1;
    while( my ($destname, $config) = each %destname2config ){
	my $data = $destname2data{$destname};

        if    ( $$config{apiver} == 1 ){
	    output_v1( $config, $data ) or $ok = undef;
        }elsif( $$config{apiver} == 2 ){
	    output_v2( $config, $data ) or $ok = undef;
        }else{
            Log $LogLvl{HTTP}, "Unknown protocol version (%d).", $$config{apiver};
	    $ok = undef;
        }
    }
    return $ok;
}

sub processThreshold {
    my ($t) = @_;
    if    ( $t =~ m/^\@?([.+-\d]+)$/ ){
	return (0, $1);
    }elsif( $t =~ m/^\@?([.+-\d]+):$/ ){
	return ($1, undef);
    }elsif( $t =~ m/^\@?~:([.+-\d]+)$/ ){
	return (undef, $1);
    }elsif( $t =~ m/^\@?([.+-\d]+):([.+-\d]+)$/ ){
	return ($1, $2);
    }else{
	return (undef, undef);
    }
}

sub processData {
    my ($raw) = @_;
    my @metric;

    # performance data format:
    #
    #   'label'=value[UOM];[warn];[crit];[min];[max] 'label'=value[UOM];[warn];[crit];[min];[max] ...
    #
    # warn / crit format:
    #
    #   range           generate an alert if
    #   10 		< 0 or > 10, (outside the range of {0 .. 10})
    #   10: 		< 10, (outside {10 .. inf })
    #   ~:10 		> 10, (outside the range of { -inf .. 10})
    #   10:20 		< 10 or > 20, (outside the range of {10 .. 20})
    #   @10:20 		>= 10 and <= 20, (inside the range of {10 .. 20})
    #                   ( but, it drops information of inside / outside )
    #   
    foreach (split /\s/, $raw) {
        my ($key,$val) = split /=/, $_, 2;
        my @data = split /;/, $val;
        Log $LogLvl{Decode}, $key."=".$data[0];
        $data[0] =~ m/([.+-\d]*)(.*)/;
        my $units = $2;
        my $factor = 1;
        $factor = 1000 if ($units eq "ms");
        $factor = 1000000 if ($units eq "us");
        $data[0] = $1/$factor;

        push @metric, { label => $key, value => $data[0] };

	next unless $GlobalConfig{enable_optional_values};

	my ($warn_lower, $warn_upper) = processThreshold( $data[1] );
	my ($crit_lower, $crit_upper) = processThreshold( $data[2] );
	my $min = $data[3];
	my $max = $data[4];

        push @metric, { label => $key, name_of_optional_value => 'warn_lower', value => $warn_lower }
		if $warn_lower ne '';
        push @metric, { label => $key, name_of_optional_value => 'warn_upper', value => $warn_upper }
		if $warn_upper ne '';
        push @metric, { label => $key, name_of_optional_value => 'crit_lower', value => $crit_lower }
		if $crit_lower ne '';
        push @metric, { label => $key, name_of_optional_value => 'crit_upper', value => $crit_upper }
		if $crit_upper ne '';
        push @metric, { label => $key, name_of_optional_value => 'min', value => $min }
		if $min ne '';
        push @metric, { label => $key, name_of_optional_value => 'max', value => $max }
		if $max ne '';
    }
    return @metric;
}

sub processDataline {
    my ($l, $regexp, $time_is_captured_as, $data_is_captured_as) = @_;

    return unless $l =~ $regexp;

    my %d = %LAST_PAREN_MATCH;
    my $time = $d{ $time_is_captured_as };
    my $data = $d{ $data_is_captured_as };
    my @metric = processData($data);
    $d{"*time"}   = $time;
    $d{"*metric"} = \@metric;

    return \%d;
}

sub processDatafile {
    my ($filename) = @_;
    my @data_of_file;

    my $perflineregexp = $GlobalConfig{perflineregexp};
    my $regexp = qr/$perflineregexp/;
    my $time_is_captured_as = $GlobalConfig{time_is_captured_as};
    my $data_is_captured_as = $GlobalConfig{data_is_captured_as};

    open DATA, '<', $filename or die "$filename: $!\n";
    Log $LogLvl{Files}, "Reading $filename ...";
    while(<DATA>) {
        chomp;
        Log $LogLvl{Data}, "$_";
        my $data_of_line = processDataline($_, $regexp, $time_is_captured_as, $data_is_captured_as);
    	push @data_of_file, $data_of_line if $data_of_line;
    }
    close DATA;
    return @data_of_file;
}

sub process {
    my $filename = $GlobalConfig{perfdata};
    checkFile("data", $filename);

    my $datafile = $filename;
    unless ($PRESERVE || $DRYRUN) {
	$datafile = $filename.".".$$;
        Log $LogLvl{Files}, "Renaming $filename to $datafile";
        rename $filename, $datafile or die "Renaming $filename: $!\n";    
    }

    my @data = processDatafile($datafile);
    my $ok = output( @data );
    unless ($ok) {
	my ($h, $i);
	open $h, '>>', "$filename.failed" and do {
	    open $i, '<', $datafile and do {
		while( <$i> ){ print $h $_; }
		close $i;
	    };
	    close $h;
	};
    }
    unless ($PRESERVE || $DRYRUN) {
        Log $LogLvl{Files}, "Removing $datafile";
        unlink $datafile;
    }
}

sub convertOldWay2NewWay {

    if( $GlobalConfig{perflineregexp} eq '' ){
    	my $delimiter = $GlobalConfig{perfdelimiter};
	my $format    = $GlobalConfig{perfformat};
	my @format = split m"$delimiter", $format;
	my @regexp;
	my $labelnum = 1;

	my @measurement;
	push @measurement, $GlobalConfig{global_prefix} if $GlobalConfig{global_prefix} eq "";
	my @tags;
	foreach my $i ( @format ){
	    if( $i =~ m"^tag=(.*)" ){
	        push @regexp, "(?<tag_$1>.*)";
		push @tags, "$1=<tag_$1>";
	    }elsif( $i eq 'label' ){
	        push @regexp, "(?<label_$labelnum>.*)";
		push @measurement, "<label_$labelnum>";
		$labelnum++;
	    }elsif( $i eq 'time' ){
	        push @regexp, '(?<time>.*)'
	    }elsif( $i eq 'data' ){
	        push @regexp, '(?<data>.*)'
	    }elsif( $i eq 'blank' ){
	        push @regexp, '(?:.*)'
	    }else{ die "perfformat: $i, stopped"; }
        }
	my $regexp = "^" . join($delimiter, @regexp) . "\\s*\$";
	$GlobalConfig{perflineregexp} = $regexp;

	my $measurement = join $GlobalConfig{delimiter}, @measurement;
	my $measurement_with_tags = join ",", $measurement, @tags;
	$ConditionalConfig[0]{measurement} = $measurement_with_tags;
	$ConditionalConfig[0]{fieldkey} = "<label>";
    }
}

my $configfile = '/etc/naemon/naemon2influx.cfg';
my $help;
my $timefactor;
my $debug;
my $perfdata;

GetOptions (
    "d|debug=s"    => \$debug,
    "f|file=s"     => \$configfile,
    "D|perfdata=s" => \$perfdata,
    "h|help"       => \$help,
    "n|preserve"   => \$PRESERVE,
    "N|dryrun"     => \$DRYRUN,
    "T|timefactor" => \$timefactor)
    or die("Error in command line arguments\n");

if ($help) {
	Usage();
	exit 1;
}

checkFile("config", $configfile);
readConfig($configfile);
cmdlineOverride($debug, $perfdata, $timefactor);

convertOldWay2NewWay();

Log ($LogLvl{Settings}, "--");
Log ($LogLvl{Settings}, "$_=$GlobalConfig{$_}") for (keys %GlobalConfig);
Log ($LogLvl{Settings}, "--");

process();

=pod

=encoding utf8

=head1 naemon2influx

Write naemon perforance data directly to InfluxDB.

=head1 SYNOPSIS

 naemon2influx [-d|--debug debug] [-D|--perfdata file]
          [-f|--file file] [-n|--preserve] [-N|--dryrun]
          [-T|--timefactor]

 naemon2influx [-h|--help]

=head1 DESCRIPTION

Nagios is great. InfluxDB is great. While you are performing service 
checks in Nagios they are (probably) creating performance data as a 
by-product. If not you might as well be. 

There are mechanisms for inserting this data into InfluxDB's time 
series database, but htere have their limitations, and most involve 
going through some form of graphite-like interface. naemon2influx 
bypasses graphite and writes directly to InfluxDB using either of 
the native udp or tcp interfaces. 

naemon2influx parses Nagios produced performance data and constructs 
InfluxDB line format metrics, readly to be set to InfluxDB's UDP 
listener (via stdout to be subsequently piped), or TCP listener.

=head1 OPTIONS

=over 8

=item B<-d>|B<--debug> B<debug level>>

This sets the verbosity level of the debug output. 

Debug output is written to stderr, it is left to the caller to 
redirect the debug output to a suitable log file.


Debug level is a bit mask. Default is 0 (silent).

  1: HTTP summary
  2: InfluxDB line format output
  4: file access
  8: configuration settings
 16: deconstruction of the perfdata line format
 32: displays raw perfdata line
 64: displays the prefdata parsed into separate metrics.

See B<Examples>.

=item B<-D>|B<--perfdata> B<file>

Specify the locaction of the naemon performancer data file.

=item B<-f>|B<--file> B<file>

Specify the locaction of the configuration file. 
Default /etc/naemon/naemon2influx.cfg.

=item B<-n>|B<--preserve>

The normal operation is to move the performance datafile to a 
new file, appending the process id to the performace data file,
and to delete this file once processing is complete.

The option processes the file in-place, leaving it intact for 
further processing. It is expected that this further processing
will delete or truncate the datfile thus ensuring that the same
data is not processes again on sebsequent runs.

=item B<-N>|B<--dryrun>

Implies B<-n>. No data is written to InfluxDB. Used for debugging
configuration.  If the configuration would ordinarily post to HTTP,
the return status code is listed as 0.

=item B<-T>|B<--timefactor> I<N>

Default 9. Nagios writes performance data timestamped by epoch seconds.
InfluxDB expects epoch nanoseconds, this is a difference of 9 orders
of magnitude. Nagios timestamps are multipled by 10 to the power
of this value.

=back

=head1 EXAMPLES

To run using a test configuration file, preserving the test datafile,
display configuration settings and decoded metrics.

naemon2influx -D perfdata.out -f naemon2influx.test -N -d 72

To write to InfluxDB's UDP listener on port 8086, while directing the
debug information (file access and configuration settings) to a log file.

naemon2influx -d12 2> /var/log/naemon2influx.log | nc -u localhost 8086

=head1 ENVIRONMENT

None.

=head1 FILES

=over 8

=item F</etc/naemon/naemon2influx.cfg>

=back

=head1 AUTHOR

Andrew Pickin

=head1 SEE ALSO

L<naemon2influx.cfg(5)>

